set(_splatter_current_edition "@SPLATTER_EDITION@")
set(_splatter_bin_dir "@SPLATTER_BIN_DIR@")
set(_splatter_lib_dir "@SPLATTER_LIB_DIR@")

function(_splatter_prepare_dir dir)
    if(dir STREQUAL "")
        return()
    endif()
    file(MAKE_DIRECTORY "${dir}")
    set(marker "${dir}/.edition")
    set(should_clean TRUE)
    if(EXISTS "${marker}")
        file(READ "${marker}" existing_marker)
        string(STRIP "${existing_marker}" existing_marker)
        if(existing_marker STREQUAL "${_splatter_current_edition}")
            set(should_clean FALSE)
        endif()
    endif()
    if(should_clean)
        message(STATUS "Clearing Splatter outputs in ${dir} for edition ${_splatter_current_edition}")
        file(GLOB children LIST_DIRECTORIES true "${dir}/*")
        if(children)
            file(REMOVE_RECURSE ${children})
        endif()
    endif()
    file(WRITE "${marker}" "${_splatter_current_edition}\n")
endfunction()

_splatter_prepare_dir("${_splatter_bin_dir}")
_splatter_prepare_dir("${_splatter_lib_dir}")
